label act3_shot_1:
    scene bg7day with eye_open
    play music morning_bgm loop
    pov "ขึ้นเดือนใหม่ เดือนสุดท้ายของปีแล้วสินะ"
    pov "เดือนนี้ก็มีอีเว้นท์อีกหลายอย่างเลย รวมถึงวันเกิดของมายด์ด้วย.."
    scene black
    with transition_4
    centered "-ไม่กี่วันก่อนหน้า-"
    scene bg8day
    with fade
    play music flashback_bgm loop fadein 0.5 fadeout 0.25
    show tsuru maid_idle2 at leftpostsuru
    with dissolve
    tsuru "นี่ผู้จัดการ กับมายด์แล้วรู้สึกยังไงหรอ"
    show  del maid_idle2 at rightposdel
    with easeinright
    debirun "นั่นสิๆ ดูเป็นห่วงพี่มายด์มากจนผิดปกติ"
    pov "คือ..ฉัน.."
    show tsuru maid_smile2 at leftpostsuru
    show del maid_abuse at rightposdel
    with dissolve
    tsuru "ชอบใช่มั้ยละคะ มายด์น่ะ"
    pov "กะ ก็ไม่ปฏิเสธหรอกนะ"
    pov "แล้วมันมีปัญหาตรงไหนล่ะ!?"
    show tsuru maid_smile4 at leftpostsuru
    tsuru "ก็ไม่มีอะไรหรอกค่ะ หนูแค่คิดว่าพี่ผู้จัดการควรจะทำอะไรได้แล้ว"
    pov "(ตอนซึรุจะพูดอะไรจริงจังจะเรียกฉันว่าพี่นี่หน่า..)"
    pov "ควรจะทำอะไร.. หมายถึงอะไรหรอ.."
    show tsuru maid_wow at leftpostsuru
    show del maid_stunned at rightposdel
    with dissolve
    tsuru "โห่ววววววว พี่นี่ซื่อบื้ออะเป่าเนี่ยยยยยย!"
    debirun "ไม่คิดเลยนะว่าผู้จัดการจะอ่อนหัดได้ขนาดนี้ หนูไม่คิดเลย"
    pov "เดี๋ยวก่อนสิ แล้วฉันควรจะทำยังไงล่ะ!? แล้วที่ว่าควรจะทำอะไรเนี่ยมันหมายถึงอะไรกัน?"
    show tsuru maid_idle2 at leftpostsuru
    with dissolve
    tsuru "ก็หมายถึง ต้องไปสารภาพรักกับมายด์ได้แล้วไง! จะปล่อยให้เวลาผ่านไปอีกเท่าไหร่กันคะ!"
    pov "ก็ที่ผ่านมาฉันเอาแต่เล่นมุกจีบแบบแกล้งๆนี่นา จะให้บอกไปว่าชอบแบบจริงจังมันก็"
    pov ".."
    show tsuru maid_idle1 at leftpostsuru
    show del maid_idle1 at rightposdel
    with dissolve
    tsuru "เฮ้ออ.. ถ้างั้นพวกหนูจะหาจังหวะให้อยู่ด้วยกันสองต่อสองแล้วกัน พี่ก็ทำให้เต็มที่ด้วยล่ะ"
    tsuru "แล้วเดี๋ยวหนูกับเดลจะเอาเค้กตา-"
    pov "สะ สะ สะ สารภาพรัก! ฉัน ฉัน ฉัน...."
    scene black
    with transition_4
    scene bg7day
    play music morning_bgm loop fadein 0.5 fadeout 0.25
    with fade
    pov "เล่นเอาหลังจากนั้นก็จำไม่ได้เลยว่าซึรุพูดว่าอะไร..."
    pov "ช่างมันละกันนะ... คงต้องหาของขวัญวันเกิดไว้ล่วงหน้าด้วย"
    show locket_open at truecenter:
        zoom 0.25
    pause 0.025
    hide locket_open
    with pixellate
    pov "เอาเป็นว่าไปซื้อของเข้าร้านก่อนดีกว่า~"
    play sound doorclose_sfx fadein 0.5
    scene bg5day with wiperight
    pov "(จะว่าไปมายด์ตื่นรึยังนะ ลองเรียกหน่อยดีกว่า)"
    scene bg22:
        zoom 1.5
        xalign 0.5
        yalign 0.6
    with wiperight
    play sound knock_sfx fadein 0.25
    pov "มายด์ มายด์ตื่นรึยัง ทานอะไรมั้ย"
    pov "(ไม่ตอบแหะ)"
    scene bg5day with wipeleft
    play sound walking_sfx fadein 0.2
    pov "งั้นฉันจะทำอะไรไว้ให้นะ อย่าลืมลงมาทานด้วยล่ะ"
    scene bg9day with dottransition
    play sound market_sfx fadein 0.15
    play music store_bgm loop fadein 0.5 fadeout 0.25
    pov "อ่าา..เอาอันนี้แล้วก็อันนี้ เอาล่ะ! ครบแล้วสินะ"
    employee "สวัสดีค่า ยินดีให้บริการค่า"
    employee "คุณลูกค้าได้รับสิทธิ หมุนการาปองค่ะ มีสิทธิได้รับตั๋วออนเซ็น 1 ครอบครัวค่า!"
    pov "อ่า..ขอบคุณนะ"
    pov "(คนไม่มีดวงอย่างฉัน จะไปหมุนได้ ได้ยังไงกันล่ะ TT)"
    show garapon_sd at truecenter
    with dissolve
    play sound garapon_sfx fadein 0.15 volume 0.75
    $ renpy.pause(3.5, hard=True)
    show garapon_win_sd at truecenter
    "หลังจากที่หมุนการาปองแบบขอไปทีนั้นเอง ก็มีลูกกลมๆสีทองกลิ้งออกมา"
    play sound "<from 5 to 8>SFX/bell_sfx.mp3" fadeout 0.25
    employee "ยินดีด้วยค่ะคุณลูกค้า! ได้รับตั๋วรางวัลพิเศษเที่ยวออนเซ็น 1 ครอบครัวค่าาาาา!......."
    stop music fadeout 1.5
    pov ".. (เอาจริงดิ)"
    scene bg2day with dottransition
    play music cafe_bgm loop fadein 0.5 fadeout 0.25
    show tsuru maid_drunk1 at middlepostsuru
    show del maid_wave at rightposdel
    with dissolve
    tsuru_debirun "ผู้จัดการสวัสดีค่า.."
    pov "อ้าว..พวกเธอมาเร็วจัง"
    pov "เอ๊ะ ทำไมซึรุตัวแดงๆ ล่ะ?"
    show del maid_idle2 at rightposdel
    show tsuru maid_drunk2 at middlepostsuru
    with dissolve
    tsuru "พอดีเมื่อคืนหนูดื่มอ่ะค่ะ แหะๆๆ"
    show del maid_idle1 at rightposdel
    with dissolve
    debirun "ส่วนเดลเลิกเรียนเร็วก็เลยมาเจอพี่ซึรุที่ตัวแดงระหว่างทางน่ะ"
    pov "ปกติก็ไม่น่าจะแดงข้ามวันนะซึรุ..."
    show tsuru maid_drunk4 at middlepostsuru
    with dissolve
    tsuru "ขอนมสักแก้วหน่อยสิพี่ผู้จัดการ เดี๋ยวหนูก็ดีขึ้น.."
    pov "จ้าๆ"
    show mild maid_wave at llposmild
    with easeinleft
    mild "สวัสดีค่า... มาเร็วจังเลยนะ"
    show del maid_smile4 at rightposdel
    show tsuru maid_drunk3 at middlepostsuru
    with dissolve
    tsuru "สวัสดีแกร๊"
    debirun "สวัสดีค่าพี่มายด์"
    pov "มายด์รอแปปนึงนะ กำลังจะทำอะไรให้ทานพอดี"
    show tsuru maid_drunk2 at middlepostsuru
    show mild maid_idle4 at llposmild
    with dissolve
    tsuru "อย่าลืมนมของหนูด้วยนะผู้จัดการรรรร"
    pov "จ้าาาา มายด์รอแปปนึงนะ เดี๋ยวจะทำอะไรให้ทาน"
    pov "พวกเธอ ฉันฝากเอาของที่ฉันซื้อมาเข้าตู้เย็นให้ที"
    show del maid_smile1 at rightposdel
    show tsuru maid_drunk1 at middlepostsuru
    show mild maid_idle2 at llposmild
    with dissolve
    mild_tsuru_debirun "ค่าาาา"
    hide del maid_smile1
    hide tsuru maid_drunk1
    hide mild maid_idle2
    with easeoutleft
    "สาวๆช่วยกันเอาของเข้าตู้เย็น"
    with fade
    show mild maid_doubt at middleposmild
    mild "เอ๊ะ อันนี้คืออะไรหรอคะ กระดาษหน้าตาแปลกๆ นี่"
    show tsuru maid_smile2 at leftpostsuru
    with moveinleft
    tsuru "หน่ายยยย"
    show tsuru maid_smile6 at bounce,leftpostsuru
    with hpunch
    tsuru "เอออออออออออ๋ นี่มันตั๋วออนเซ็นพร้อมที่พักฟรี สำหรับ 4 คนนี่นาาา"
    show del maid_smile3 at rightposdel
    with easeinright
    debirun "จริงด้วยยย ผู้จัดการได้มาหรออออ"
    pov "อืมม.. จะว่ายังไงดี ก็ใช่แหละตอนนี้ยังคิดอยู่เลยว่าดวงปีนี้ฉันหมดแล้วรึป่าวนะ"
    show tsuru maid_smile5 at bounce,leftpostsuru
    with dissolve
    tsuru "นี่ๆ จะว่าไปมันมีสวนสัตว์อยู่จังหวัดนั้นด้วยนี่นาผู้จัดการ"
    pov "จะว่าไปก็จริงนะ.."
    show del maid_smile6 at bounce,rightposdel
    with dissolve
    debirun "งั้นเราไปสวนสัตว์ด้วยเลยมั้ยผู้จัดการ"
    debirun "พี่มายด์จะได้เห็นแพนด้าตัวจริงด้วย"
    pov "มายด์ว่ายังไงล่ะ อยากไปมั้ย"
    hide del maid_smile6
    hide tsuru maid_smile5
    with None
    scene bg2day:
        crop (100,200,1480,720)
        size (1920, 1080)
    show mild maid_smile9 at bounce,mildunhood_close:
        zoom 0.65
    with zoomout
    mild "ปะ ไปค่ะ ได้มั้ยคะ [povname]"
    pov "แน่นอนสิ ถ้าเธออยากไปฉันก็จะพาเธอไป"
    tsuru_debirun "ไชโยย~"
    stop music fadeout 0.75
    scene black with fade
    pause 1.0
    jump act3_shot_2

label act3_shot_2:
    scene bg10day with dottransition
    play music zoo_bgm loop fadein 0.5 fadeout 0.25
    show mild normal_smile12 at middlemildnormal
    show del normal_idle1 at rightposdel
    show tsuru normal_idle2_1 at leftpostsuru
    with dissolve
    pov "อากาศดีจังเลยนะ"
    pov "(ชุดที่เลือกให้วันนั้นสินะ เหมาะมากเลย น่ารักสุดๆ)"
    show mild normal_smile8 at bounce,middlemildnormal
    mild "ตื่นเต้นจังเลยนะคะ"
    pov "งั้นเราไปกันเถอะ"
    show tsuru normal_smug at leftpostsuru
    with dissolve
    tsuru "อ่ะพี่ [povname] เดี๋ยวหนูกับเดลจะไปดูโชว์สัตว์น้ำนะ ฝากพามายด์เดินเที่ยวด้วยนะคะ"
    hide tsuru normal_smug
    hide del normal_idle1
    with moveoutleft
    show mild normal_blush1 at middlemildnormal
    pov "ดะ เดี๋ยววววว ซึรุ๊ววววว เดลลลลลลลลลลล"
    pov "(ยัยพวกนั้นตั้งใจให้ฉันอยู่กับมายด์แค่สองต่อสองสินะ)"
    show mild normal_doubt at middlemildnormal
    show emotion_question at emotepos
    with dissolve
    mild "[povname] มีอะไรรึเปล่าคะ?"
    pov "ปะ เปล่า งั้นเราไปกันเลยมั้ย"
    scene bg10day:
        crop (300,200,1280,720)
        size (1920, 1080)
    show mild normal_idle2 at mildunhood_close
    with dissolve
    mild "ค่าาาา"
    stop music fadeout 1.0
    scene bg18day:
        crop (800,200,1600,720)
        size (1920, 1080)
    play sound animal_bgm loop fadein 1.0 volume 0.8
    show rabbit at bounce,animal
    with dottransition
    show mild normal_idle1 at mildhood
    with easeinright
    play music zoo_bgm loop volume 0.25 fadein 1.0 fadeout 1.0
    mild "ว้าววว ดูสิๆ น่ารักจังเลยค่ะ!"
    show rabbit at bounce,animal
    pov "กระต่ายน่ะปกติตัวเล็กแต่มีขาที่แข็งแรงมากๆ ในการกระโดด มีหูที่ยาว ใช้ฟังเสียงได้ดีเลยล่ะ"
    show rabbit at bounce,animal
    show mild normal_smile3 at mildhood
    mild "ว้าววว จริงด้วยหูยาวๆ ดูนุ่มนิ่มจังเลย"
    show rabbit at bounce,animal
    show mild normal_smile8 at mildhood
    mild "อ่ะ!  นั่นมัน!"
    show rabbit at bounce,animal
    hide mild normal_smile8
    with moveoutright
    show rabbit at bounce,animal
    pov "หื้ม รอด้วยสิมายด์~~~"
    scene bg18day:
        crop (400,200,800,720)
        size (1920, 1080)
    show capybara at bounce,animal:
        xalign 0.8
        zoom 1.3
    with wipeleft
    show mild normal_smile13 at leftmildnormal
    with easeinleft
    mild "น้องตัวนี้ตัวใหญ่เบิ้มเลยค่ะ!"
    pov "(ใครสอนพูดแบบนั้นกันนะ)"
    pov "ตัวนี้คือ คาปิบาร่า เป็นหนูที่ตัวใหญ่ที่สุดในโลก น้องชอบแสดงสีหน้านิ่งๆ มีดวงตากลมโตและจมูกฟุดฟิดน่ารัก จึงทำให้ผู้คนตกหลุมรักเสมอเลย แถมยังใจดีกับทุกๆ สิ่งด้วยนะ"
    show capybara at bounce,animal:
        xalign 0.8
        zoom 1.3
    show mild normal_smile11 at leftmildnormal
    with dissolve
    mild "{size=*0.5}เหมือน [povname] สินะคะ ที่ว่าใจดี{/size}"
    pov "มายด์พูดว่าอะไรนะ?"
    show mild normal_blush1 at leftmildnormal
    with dissolve
    mild "ปะ เปล่าค่ะ! เราไปต่อไปเลยมั้ยคะะ"
    hide mild normal_smile8
    with moveoutright
    scene bg18day:
        crop (0,0,1080,720)
        size (1920, 1080)
    show hornbill at animal:
        xalign 0.08
        ypos 180
        zoom 1.3
    with wiperight
    show mild normal_smile8 at mildhood
    with easeinright
    play sound bird_sfx fadein 0.125
    pov "มายด์ระวังง!"
    show mild normal_flustered at bounce,mildhood
    with hpunch
    pov "เป็นอะไรมั้ย"
    stop sound
    show mild normal_idle2 at bounce,mildhood
    with dissolve
    mild "ไม่เป็นไรค่ะ แล้วเมื่อกี้"
    pov "นกเงือกน่ะ เป็นสัตว์ที่หายากในปัจจุบันแล้วล่ะนะ จุดเด่น คือ จะงอยปากหนาที่ใหญ่และมีโหนกทางด้านบน"
    pov "แล้วก็ๆ รู้มั้ยเจ้านกตัวนี้มีลักษณะพิเศษด้วยนะรู้รึเปล่า มายด์ลองเดาดูมั้ย"
    show mild normal_doubt at mildhood
    show emotion_question at emotepairpos2
    with dissolve
    mild "เอ๋ ชอบอยู่ตัวเดียวหรอคะ แบบว่าไม่ชอบสุงสิงกับใคร?"
    pov "อื้มม จริงๆ แล้ว ความพิเศษของนกเงือก เป็นนกที่มีนิสัยรักเดียวใจเดียว นกเงือกเลยถูกให้เป็นสัญลักษณ์แห่งรักแท้อันบริสุทธิ์ ว่ารักฉันจะรักเธอแค่คนเดียวจนตัวตายเลยน่ะ"
    scene bg18day:
        crop (540,0,540,720)
        size (1920, 1080)
    show mild normal_blush1 at mildunhood_close
    with dissolve
    pov "มายด์? เป็นอะไรรึป่าว หน้าดูแดงๆ มีไข้หรอ"
    menu:
        "เอามือไปวัดที่หน้าผาก":
            $ honey_score += 1

        "เอาหน้าผากวัดไปเลย!":
            $ honey_score -= 1
    play sound walking_sfx
    hide mild normal_blush1
    with easeoutright
    pause 1.0
    stop sound
    mild "มะ ไม่เป็นอะไรค่ะ!"
    pov "(หรือฉันทำอะไรไม่ดีไปรึเปล่านะ เอาเป็นว่าเดินตามไปก่อนละกัน)"
    scene bg18day:
        crop (600,200,1000,720)
        size (1920, 1080)
    with wiperight
    pov "(หื้ม ทำไมมายด์ถึงหยุดเดินกันนะ)"
    pov "อ๋อ ในที่สุดก็เจอแล้วสินะ"
    scene bg18day:
        crop (600,200,1000,720)
        size (1920, 1080)
        linear 2.0 crop (1000, 200, 1600, 720)
    with dissolve
    pause 2.0
    show panda at bounce,animal:
        xalign 0.8
        zoom 1.3
    pov "(ฉันมายืนอยู่ข้างๆ โดยที่ไม่พูดอะไร)"
    show mild normal_smile9 at leftmildnormal
    with dissolve
    mild "(พี่แพน)" 
    show mild normal_smile8 at leftmildnormal
    with dissolve
    mild "[povname] คะ ขอบคุณนะคะ"
    pov "อะ อื้มม ด้วยความยินดี"
    pov "(บ้าเอ้ยน่ารักเป็นบ้าเลย)"
    scene bg18day:
        crop (1000, 200, 1600, 720)
        size (1920, 1080)
        linear 1.0 crop (0, 0, 1920, 720)
    with dottransition
    show mild normal_smile8 at middlemildnormal
    with dissolve
    show tsuru normal_idle2_2 at leftpostsuru
    with easeinleft
    tsuru "[povname] คะะะ มายยยยยยยยยยย"
    pov "ว่าไงซึรุ แล้วเดลไปไหนล่ะ"
    scene bg18day
    with fade
    show del_lili at truecenter:
        zoom 0.7
    tsuru "อ่อ พอดีเดลไปเจอเพื่อนสนิทเขาน่ะ เลยขอคุยกันก่อน"
    scene bg18day
    show mild normal_smile8 at middlemildnormal
    show tsuru normal_idle1_2 at leftpostsuru
    with fade
    pov "แล้วเป็นยังไงบ้างไปดูโชว์"
    show tsuru normal_idle2_1 at leftpostsuru
    with dissolve
    tsuru "โชว์สัตว์น้ำก็ยังน่ารักเหมือนเดิมนะคะ"
    tsuru "แล้วก็ออกไปดูฮิปโป"
    show mild normal_smile12 at middlemildnormal
    with dissolve
    show del normal_smile2 at rightposdel
    with easeinright
    debirun "แล้วก็ไปดูค้างคาวมาค่ะ!"
    show tsuru normal_doubt at leftpostsuru
    with dissolve
    tsuru "คุยกับเพื่อนเสร็จแล้วหรอเดล"
    show del normal_idle1 at rightposdel
    with easeinright
    debirun "เสร็จแล้วครับพี่"
    show del normal_doubt1 at rightposdel
    with easeinright
    debirun "แล้วพี่มายด์เป็นไงบ้าง สนุกมั้ยคะ"
    show mild normal_idle2 at middlemildnormal
    with dissolve
    mild "สนุกมากเลยยย"
    show tsuru normal_smile5 at leftpostsuru
    show del normal_smile8 at rightposdel
    with dissolve
    tsuru "ดีจังเลยที่เธอยิ้มแบบนี้"
    tsuru "ยิ้มบ่อยๆนะมายด์"
    show mild normal_smile7 at middlemildnormal
    with dissolve
    mild "อื้ออ"
    show del normal_doubt2 at rightposdel
    with dissolve
    debirun "ยังพอมีเวลาเราไปเดินกันต่อสักหน่อยมั้ยคะ"
    pov "นั่นสินะ ไปกันเถอะมายด์ยังมีอะไรให้ดูอีกเยอะเลยนะ"
    scene bg18day:
        crop (400, 200, 800, 720)
        size (1920, 1080)
    show mild normal_smile13 at mildunhood_close
    with fade
    menu:
        "จับมือมายด์ แล้วเดินไปด้วยกัน":
            $ honey_score += 1
            scene bg18day:
                crop (400, 200, 800, 720)
                size (1920, 1080)
                linear 1.0 crop (800, 200, 1800, 720)
            hide mild normal_smile13
            with easeoutright

        "วิ่งนำไปก่อนเลย":
            $ honey_score -= 1
            scene bg18day:
                crop (400, 200, 800, 720)
                size (1920, 1080)
                linear 1.0 crop (800, 200, 1800, 720)
            with wipeleft
    play sound running_sfx fadein 0.5
    tsuru_debirun "รอด้วยสิคะะะะะะ!"
    stop sound
    scene black
    with fade
    centered "แล้วเวลาก็ผ่านไป ช่วงเย็นๆ พวกเราก็มาถึงที่ออนเซ็น"
    scene bg11eve with dottransition
    play music walking_bgm loop fadein 0.5 fadeout 0.25
    show del normal_gloomy2 at rightposdel
    with dissolve
    debirun "มาถึงสักที หนูหิวจะตายแล้วพี่"
    show tsuru normal_grumpy2 at leftpostsuru
    with dissolve
    tsuru "โถ่ เดล แทบไม่ช่วยพี่ๆ ดูทางเลยนะ"
    show del normal_doubt2 at rightposdel
    with dissolve
    debirun "ก็เดลไม่รู้ทางนี่นาพี่ซึรุ"
    pov "เอาน่าซึรุ ยอมๆน้องเถอะนะ"
    pov "คืนนี้ฝากพวกเธอดูแลมายด์ด้วยนะ"
    show tsuru normal_idle1_2 at leftpostsuru
    with dissolve
    tsuru "ได้เลยค้าบบบ"
    scene bg11eve
    show mild normal_flustered at mild_close
    with dissolve
    mild "ระ รบกวนด้วยนะคะ"
    scene bg11eve
    show tsuru normal_smile5 at leftpostsuru
    show del normal_smile8 at rightposdel
    show mild normal_flustered at middlemildnormal
    with dissolve
    debirun "ไม่ต้องตื่นเต้นหรอกนะพี่"
    show mild normal_smile8 at middlemildnormal
    mild "ขอบคุณนะเดลจัง"
    scene bg12eve with dottransition
    show del onsen_idle1 at leftposdel
    show tsuru onsen_idle2 at rightpostsuru
    with dissolve
    debirun "ว้าวว วิวดีจัง!"
    show del onsen_smile2 at leftposdel
    with dissolve
    debirun "ไม่นึกว่าจะได้ที่พักดีๆ แบบนี้นะเนี่ย!"
    show tsuru onsen_smile1 at rightpostsuru
    with dissolve
    tsuru "นั่นสินะดูเงียบสงบเหมาะสำหรับการพักผ่อนมากเลย"
    show tsuru onsen_smile2 at rightpostsuru
    with dissolve
    tsuru "งั้นเราเตรียมตัวไปกินข้าวกันเถอะ!"
    show del onsen_smile1 at bounce,leftposdel
    with dissolve
    debirun "เย้~"
    show tsuru onsen_stunned at rightpostsuru
    with dissolve
    tsuru "ป่ะ ไปกันนะมายด์~"
    scene bg12eve
    show mild onsen_blush1 at mildunhood_close
    with fade
    mild "ค่า!"
    hide mild onsen_blush1
    with easeoutleft
    pov "(สาวๆ ดูจะสนุกน่าดูเลยนะ ฉันเองก็พักผ่อนบ้างดีกว่า)"
    scene black
    with fade
    stop music fadeout 1.5
    centered "หลังจากที่สาวๆ ทานอาหารเสร็จก็ได้ไปแช่ออนเซ็นกันต่อ"
    scene cg3:
        crop (804, 790, 1920, 1080)
        size (1920, 1080)
    with eye_open
    play music onsen_bgm loop fadein 1.0
    debirun "อิ่มจังเลยน้า~ อาหารก็อร่อย เที่ยวสวนสัตว์ก็สนุกสุดๆ เลย"
    tsuru "นั่นสินะ เดลดูสนุกสุดๆ เลยนี่นา"
    tsuru "มายด์ล่ะ เป็นไงบ้าง สนุกมั้ย อาหารอร่อยรึเปล่า"
    scene cg3:
        crop (804, 790, 1920, 1080)
        size (1920, 1080)
        linear 2.5 crop (0,0,3468,1946)
    with dissolve
    $ show_quickmenu = False
    $ renpy.pause(2.5,hard=True)
    $ show_quickmenu = True
    mild "สนุกมากเลยค่ะ อาหารก็อร่อยเหมือนที่ [povname] ทำให้ทานเลยค่ะ"
    scene cg3
    with dissolve
    tsuru "งั้นหรอ.."
    tsuru "แล้วเที่ยวกับ [povname] วันนี้เป็นยังไงบ้าง"
    scene cg3:
        xpos -1500
    with zoomin
    mild "ตื่นเต้นแล้วก็น่าสนใจมากเลยค่ะ พึ่งได้เจอสัตว์หลากหลายชนิดขนาดนี้เป็นครั้งแรก"
    mild "ไม่คิดว่าโลกภายนอกจะมีอะไรมากมายขนาดนี้"
    scene cg3:
        crop (1500, 0, 1920, 1080)
        size (1920, 1080)
        linear 1.5 crop (1900,0,3400,800)
    with dissolve
    mild "วันนี้คงเป็นอีกวันที่ฉันจะไม่มีวันลืมเลยค่ะ"
    mild "ยิ่งตอนที่ [povname] แนะนำสัตว์แต่ละตัวให้ฟัง เหมือน [povname] จะชอบนกเงือกมากเลยค่ะ"
    scene cg3
    with dissolve
    debirun "แล้วพี่รู้รึเปล่า ความหมายของนกเงือกน่ะ"
    scene cg3:
        crop (804, 790, 1920, 1080)
        size (1920, 1080)
        linear 1.5 crop (0,0,3468,1946)
    with dissolve
    mild "กะ ก็รู้จาก [povname] นั่นแหละ ไม่นึกว่าความหมายจะ.."
    tsuru "รักเธอแค่คนเดียวจนวันสุดท้ายสินะ"
    tsuru "แล้วเห็นพูดถึง [povname] บ่อยเลย รู้สึกยังไงกับ [povname] บ้างล่ะ ชอบเขารึเปล่า?"
    mild "ชะ ชอบหรอคะ ชอบในที่นี้.."
    tsuru "ชอบที่หมายถึง เป็นคนที่เราอยากดูแลและอยากอยู่เคียงข้างเขาไปตลอดชีวิตน่ะ"
    mild "กะ ก็ไม่ได้ไม่ชอบ [povname] หรอกนะแต่เรื่องแบบนี้.."
    tsuru "ก็อย่างเช่น อยากอยู่ใกล้ๆ เวลาทำอะไรก็จะนึกถึงเขา ว่าเขาจะชอบมั้ยนะ เป็นห่วงเป็นใยเขามากๆ อาการพวกนี้ แกมีบ้างรึเปล่าล่ะ"
    tsuru "ถ้าไม่มีก็ไม่เป็นอะไรหรอกนะ ฉันก็ถามเฉยๆน่ะ"
    debirun "อ่อ จริงด้วย พี่ซึรุคือเมื่อกี้อ่ะนะ ยัยไข่บอกว่า .."
    show white with fade
    scene bg12night_on with dissolve
    show mild onsen_blush1 at mildunhood_close
    with dissolve
    play music roman_bgm loop fadein 0.5 fadeout 0.25
    pov "อ่ะ! มายด์ ไปแช่น้ำมาเป็นไงบ้าง"
    show mild onsen_flustered at bounce,mildunhood_close
    mild "คะ คะ [povname]"
    pov "เป็นอะไรมั้ย ทำไมหน้าแดงล่ะ แช่น้ำนานไปรึป่าว"
    menu:
        "เข้าไปดูอาการ":
            $ honey_score += 1

        "ยืนกระวนกระวายอยู่เฉยๆ":
            $ honey_score -= 1
    show mild onsen_blush2 at mildunhood_close
    with dissolve
    mild "มะ ไม่มีอะไรค่ะ แล้ว [povname] กำลังจะออกไปไหนหรอคะ"
    pov "อ่อ กำลังจะไปซื้อน้ำ เธอจะไปด้วยมั้ย"
    pov "ดื่มอะไรสักหน่อยจะได้ดีขึ้นนะ"
    show mild onsen_blush1 at mildunhood_close
    with dissolve
    mild "อะ โอเคค่ะ ขอฉันไปด้วยนะคะ"
    pov "อื้มม ไปกัน"
    scene bg13night with wipeleft
    pov "มายด์เอาน้ำอะไรมั้ย"
    show mild onsen_smile5 at mildunhood
    with easeinleft
    mild "นะ น้ำเปล่าก็ได้ค่ะ"
    show mild onsen_blush1 at mildunhood
    with easeinleft
    mild "คะ คือ เรานั่งคุยกันก่อนได้มั้ยคะ"
    pov "หื้มได้สิ"
    pov "(อะ อะไรกันท่าทางที่ดูน่ารักนั่น)"
    show mild onsen_smile6 at mildunhood_close
    with dissolve
    mild "วันนี้ขอบคุณมากเลยนะคะ"
    pov "ระ เรื่องอะไรหรอ"
    show mild onsen_smile5 at mildunhood_close
    with dissolve
    mild "ที่ดูแลฉันมาตลอด ทั้งความใจดี ความเอาใจใส่ที่มีให้ ฉันดีใจมากๆเลยค่ะ"
    show mild onsen_smile4 at mildunhood_close
    with dissolve
    mild "[povname] คะ ฉัน...."
    show mild onsen_blush2 at mildunhood_close:
        zoom 1.5
    with dissolve
    mild "คือ ฉัน.."
    pov "(อะไรกัน น่ะ น่ารัก! นี่มันอีเว้นท์อะไรเนี่ย..)"
    show mild onsen_blush2 at mildunhood_close:
        ypos 750
        zoom 1.5
    with easeoutbottom
    pov "บะ แบบว่า..."
    show mild onsen_sleep at mildunhood_close:
        ypos 750
        zoom 1.5
    with dissolve
    pov "(อะ เอ๊ะ อะไรกัน! ทำไมอยู่ๆมาซบล่ะ!!)"
    pov "เอิ่มม มายด์ คะ คือเป็นอะไรรึเปล่า"
    pov "นะ นี่"
    show mild onsen_sleep at mildunhood_close
    with easeinbottom
    pov "หลับหรอกหรอ.."
    pov "(อุ้มกลับไปส่งแล้วกัน)"
    scene bg12night_on with dottransition
    show tsuru onsen_stunned at middlepostsuru
    with dissolve
    tsuru "อ่ะ พี่ [povname] มายด์หลับแล้วหรอ"
    pov "อื้ม ฝากดูแลด้วยนะ"
    show tsuru onsen_smile2 at middlepostsuru
    with dissolve
    tsuru "ได้ค่าาา หลับไม่ฝันนะคะพี่"
    pov "อื้มม เหมือนกันนะ"
    pov "พรุ่งนี้ก็กลับแล้วสินะ เอาล่ะไปนอนดีกว่าา"
    stop music fadeout 2.5
    scene black with eye_shut
    pause 2.5
    jump act3_shot_3

label act3_shot_3:
    window hide
    scene bg2night
    play music cafe_bgm loop fadein 0.5 volume 0.6
    with dissolve
    pause 1.5
    scene bg2night
    show black:
        alpha .5
    pause 1
    pov "ยัยเด็กพวกนั้นนี่นะ.. ถึงจะเตี๊ยมกันแล้วก็เถอะ แต่ไม่คิดจะส่งสัญญาณบอกกันเลยรึไง?"
    pov "ถ้าฉันหัวใจวายขึ้นมาจะทำไงเล่า!? ช่างมันเถอะ.."
    stop music fadeout 0.5
    play sound drop_sfx
    pov "เอาเป็นว่าไปเตรียมอาหารก่อนแล้วกั- (ตึ้ง!!)"
    play music drama_bgm loop fadein 0.75
    pov "เสียงอะไรน่ะ! มาจากชั้นบนหนิ มายด์ทำอะไรตกรึป่าวนะ.."
    play sound walking_sfx
    scene bg5night with walkingVertical
    centered "หลังจากนั้น [povname] ก็เดินขึ้นไปชั้นบน เพื่อไปดูที่ห้องมายด์เผื่อว่ามายด์จะทำอะไรตก"
    play sound knock_sfx
    show bg21:
        zoom 1.5
        xalign 0.5
        yalign 0.6
    pov "มายด์ เธอได้ทำอะไรตกรึป่าว?"
    centered ".."
    play sound knock_sfx volume 1.5
    pov "มายด์..! เธอเป็นอะไรรึป่าว?"
    centered ".."
    play sound knock_sfx volume 1.8
    pov "มายด์อาร์!! เธอยังโอเคดีใช่มั้ย!?"
    pov "มายด์! รอฉันก่อนนะ!"
    play sound opendoor_sfx fadein 0.85
    scene bg7night with runningVertical
    $ renpy.pause(1.5,hard=True)
    play sound drawer_sfx
    queue sound key_sfx
    $ renpy.pause(3,hard=True)
    scene bg5night with runningVertical
    play sound running_sfx
    pause 1.5
    scene bg21:
        zoom 1.5
        xalign 0.5
        yalign 0.6
    play sound unlock_sfx
    queue sound opendoor_sfx fadein 0.95
    $ renpy.pause(10,hard=True)
    show bg21:
        zoom 1.5
        xalign 0.5
        yalign 0.6
    with vpunch
    pause 0.5
    scene bg6night
    show mild pajamas_sad at mild_close
    show black:
        alpha .5
    pov "(พอฉันเปิดประตูออกมาพบกับมายด์ที่นั่งทรุดอยู่ที่พื้น)"
    pov "มายด์!"
    mild "[povname].."
    pov "ฉันเอง.. ฉันอยู่นี่แล้ว.."
    scene cg4:
        size (1920,1080)
    show black:
        alpha .5
    mild "อือ.."
    pov "นี่เธอ.. กลัวความมืดหรอ?"
    mild "อือ.."
    pov "(เสียงร้องอันแผ่วเบา ร่างเล็กๆที่สั่นระริกของมายด์ถือเป็นคำตอบที่สมเหตุสมผล ว่าทำไมเธอถึงอยู่ในสภาพแบบนี้ได้)"
    pov "ขอโทษนะ ฉันไม่นึกว่าจะเธอกลัวมากขนาดนี้.."
    mild "มะ ไม่เป็นไร"
    pov "ไม่เป็นไรไม่ได้หรอกนะ.."
    play sound head_sfx fadein 0.25
    "ลูบหัวมายด์"
    pov "ฉันขอโทษจริงๆ นะ ฉันสัญญาว่าจะไม่ปล่อยให้เรื่องแบบนี้เกิดขึ้นอีก"
    pov "ฉันอยู่ตรงนี้กับเธอนะ"
    window hide
    scene cg4:
        linear 2.5 xpos -650 ypos -220
    show black:
        alpha .45
    with dissolve
    $ show_quickmenu = False
    $ renpy.pause(3.5,hard=True)
    $ show_quickmenu = True
    mild "[povname].."
    pov "มายด์ ถ้าเธอไม่ว่าอะไร.. ช่วยเล่าเหตุผลที่เธอกลัวความมืดให้ฉันฟังหน่อยได้มั้ย"
    mild "[povname].. อยากจะฟังจริงๆ หรอ.. [povname] อาจจะกลัวมายด์ไปเลยก็ได้.."
    pov "ไม่มีทางเป็นแบบนั้นหรอก ตลอดมาฉันไม่เคยกลัวเธอเลยนะ"
    mild "ถะ ถ้า [povname] พูดมาถึงขนาดนั้น.."
    play music dark_bgm loop fadein 0.75 fadeout 1.25
    scene bg24 with fade
    pause 1.5
    researcher "มันอาจจะเจ็บนิดหน่อยนะสาวน้อย"
    researcher "โครงสร้าง DNA ของเธอมันช่างเหมาะกับการทดลองเสียจริง"
    researcher "ถ้าเป็นเธอล่ะก็ การทดลองสร้างมิวแทนต์ที่สมบูรณ์แบบจะต้องสำเร็จอย่างแน่นอน"
    mild "มะ ไม่เอาค่ะ.."
    researcher "โธ่~ สาวน้อย เธอจะได้รับร่างกายที่แข็งแกร่ง เพียงแค่เธอ~ ยอมให้ฉันดัดแปลง DNA ของเธอนิดหน่อย~"
    mild "ไม่ไม่ ไม่ไม่ ไม่ไม่ ไม่เอานะคะ ไม่เอา อย่า-"
    scene black
    with eye_open
    centered "{=down_text}(มายด์สะดุ้งตื่นด้วยความหวาดกลัวและระแวง){/down_text}"
    centered "{=down_text}(ร่างกายสาวตัวเล็กที่สะดุ้งตื่นจากฝันอันแสนเลวร้ายที่ตัวของเธอไม่อยากจะจดจำ และรับรู้ถึงมันเสียด้วยซ้ำ){/down_text}"
    centered "{=down_text}(ร่างกายที่สั่นเครือของเธอแสดงถึงความหวาดระแวงและความหวาดกลัวได้อย่างชัดเจน){/down_text}"
    pov "..(นี่เธอ.. ผ่านอะไรมาเยอะขนาดนี้ด้วยร่างกายเล็กๆนั้น....)"
    play music drama_bgm loop fadein 1.0 fadeout 0.5
    scene cg4:
        xpos -650 ypos -220
    show black:
        alpha .4
    with fade
    mild "เรื่องมันก็มีแค่เท่านี้แหละค่ะ.."
    pov "มายด์.."
    show black:
        alpha .45
    with dissolve
    mild "คะ? [povname]..กลัวมายด์จริงๆสินะค-"
    scene cg4:
        size(1920,1080)
    show black:
        alpha .25
    with dissolve
    play music hug_bgm loop fadein 1.0 fadeout 2.0
    pov "อย่าพูดแบบนั้นสิ.."
    pov "แม้ฉันจะรู้แล้วว่าเธอมีที่มายังไง แต่ฉันก็ยังรู้สึกกับเธอเหมือนเดิมนะ"
    pov "ฉันเสียใจมาก ฉันทำให้เธอกลัวและนึกถึงเรื่องราวแย่ๆ"
    scene bg6night
    show mild pajamas_smile1 at mildunhood_close
    show black:
        alpha .25
    mild "[povname] คะ มองหน้ามายด์นะ ไม่ต้องรู้สึกผิดสิ"
    show mild pajamas_smile2 at mildunhood_close
    mild "เห็นมั้ยคะ มี [povname] อยู่ เค้ากลัวน้อยลงเยอะเลยค่ะ"
    show mild pajamas_smile5 at mildunhood_close
    hide black
    show black:
        alpha .15
    mild "เลิกทำหน้าเศร้าได้แล้วนะคะ"
    pov "นี่มายด์.."
    show mild pajamas_flustered1 at mildunhood_close
    pov " ฉันน่ะ.. มีเรื่องที่อยากจะบอกกับเธอมานานแล้วล่ะ ตั้งแต่วันที่ได้พบเธอ ฉันก็เริ่มรู้สึกแบบนี้มาตลอดเลย เป็นความรู้สึกที่ฉันเองก็ไม่เข้าใจหรอก แต่ว่าตอนนี้..."
    menu:
        "ปลอบมายด์":
            $ honey_score -= 1
            pov "อยากให้เธอรู้ว่าเธอมีทุกคนอยู่นะ"

        "บอกความในใจ":
            $ honey_score += 1
            pov "ฉันมั่นใจแล้ว ว่าฉันน่ะรักเธอ"
            if honey_score == 8:
                $ persistent.secret_cg = True
                scene cg5
            mild "ฉันเอง.. ฉันเองก็..."
            pause 5.0

    stop music fadeout 0.25
    scene bg6night
    with vpunch
    show tsuru normal_smile2 at middlepostsuru
    show del normal_smile5 at leftposdel
    show cake at cake
    with easeinleft
    tsuru_debirun "เซอร์ไพรส์~! แฮปปี้เบิร์ดเดย์นะมายด์"
    play music bright_bgm loop fadein 1.0 fadeout 2.0
    scene bg6night:
        crop (200, 120, 1400, 920)
        size (1920, 1080)
    show tsuru normal_dark at tsuru_close
    with dissolve
    tsuru "เอ๋~ ทั้งสองคนทำอะไรกันหรอคะ?"
    scene bg6night:
        crop (100, 120, 1200, 920)
        size (1920, 1080)
    show del normal_police at del_close
    with dissolve
    debirun "ตำรวจ.."
    scene bg6night
    show tsuru normal_idle1 at middlepostsuru
    show del normal_stunned2 at leftposdel
    show cake at cake
    pov "ทะ ทั้งคู่ๆ กำลังเข้าใจผิดนะ พะ พวกเราไม่ได้ทำอะไรกันทั้งนั้น"
    pov "อะ เอาเป็นว่ารีบเปิดไฟก่อนเถอะหน่า มายด์เขากลัวความมืด"
    scene bg6light with fade
    show tsuru normal_doubt at middlepostsuru
    show del normal_doubt1 at leftposdel
    show mild pajamas_idle2 at rrposmild
    debirun "อ้าว! แลัวทำไมผู้จัดการถึงไม่บอกกันตั้งแต่แรกก"
    pov "ฉันก็พึ่งรู้เหมือนกันนี่แหละ"
    show tsuru normal_sorry at middlepostsuru
    with dissolve
    tsuru "แก.. ฉันขอโทษนะที่ไม่ได้ตรวจสอบให้ดีก่อน.."
    show del normal_sorry at leftposdel
    with dissolve
    debirun "เดลก็ขอโทษนะพี่มายด์ คราวหลังเดลจะถามก่อน.."
    show mild pajamas_flustered1 at rrposmild
    mild "มะ ไม่เป็นไรหรอก.. ฉันผิดเองแหละที่ไม่ได้บอกทุกคน"
    pov "มายด์ เธอน่ะไม่ผิดหรอกนะ ปกติไม่มีใครเค้าพูดถึงสิ่งที่ตัวเองกลัวกันนักหรอก"
    show mild pajamas_doubt at rrposmild
    with dissolve
    mild "งะ งั้นหรอคะ?"
    pov "อืม.. งั้นเอาเป็นว่าพวกเรามาฉลองวันเกิดกันดีกว่า"
    show emotion_question at bounce,emotepairpos3
    mild "เอ๊ะ? วันเกิดใครหรอคะ?"
    show tsuru normal_smile4 at middlepostsuru
    show del normal_police at leftposdel
    with sshake
    hide emotion_question
    pov_tsuru_debirun "ก็วันเกิดเธอไง.."
    debirun "อย่าบอกนะว่าพี่มายด์.. แก่จนจำไม่ได้แล้วหรอ"
    show mild pajamas_angry2 at bounce,rrposmild
    show emotion_angry at emotepairpos3
    mild "ไอ้เดล! ฉันแค่ลืมเฉยๆ เถอะ"
    pov "จ้าๆ ไว้ไปจัดการกันทีหลังนะ ก่อนที่เทียนจะละลายหยดใส่เค้ก"
    scene bg6light
    play music hbd_bgm loop fadein 1.0 fadeout 2.0
    show mild pajamas_smile2 at mildunhood_close
    show cake at center:
        zoom 0.32
    with fade
    pov "เอาเลยมายด์ เป่าเทียนได้เลย"
    mild "*สูดหายใจเข้า*"
    play sound blow_sfx fadein 0.25 fadeout 0.5 volume 0.35
    show mild pajamas_tsundere at mildunhood_close
    hide cake
    show cake2 at center:
        zoom 0.32
    with dissolve
    pov "มายด์ฉันมีอะไรที่เตรียมมาให้เธอด้วยนะ"
    hide cake2
    with easeoutbottom
    show locket_close at center:
        zoom 0.32
    with easeinbottom
    show mild pajamas_doubt at mildcloseleft
    with easeoutleft
    mild "มันคืออะไรหรอคะ?"
    pov "เป็นของที่ฉันซื้อมาเอง เพราะคิดว่าน่าจะเหมาะกับเธอดีน่ะ แต่ถ้าเธอไม่ชอบล่ะก็จะไม่ใช้ก็ได้นะ"
    hide locket_close
    show locket_open at center:
        yalign 0.5
        zoom 0.32
    with dissolve
    show mild pajamas_smile6 at mildcloseleft
    with dissolve
    mild "สวยจัง!"
    hide locket_open
    show mild pajamas_idle2 at mildunhood_close
    with dissolve
    mild "[povname] จะให้เค้าจริงๆหรอ"
    pov "แน่นอนสิ มายด์"
    scene bg6light
    show mild pajamas_smile5 at mildunhood_close
    with dissolve
    mild "ขะ-ขอบคุณนะ ที่ทำเพื่อเค้าขนาดนี้.."
    pov "ด้วยความยินดีเลยล่ะ"
    scene black
    with fade
    $ persistent.gameclear = True
    jump end_credit
